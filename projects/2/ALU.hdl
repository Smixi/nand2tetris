// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //ZX
    Mux16(a=x, b=false, sel=zx, out=zxOut);
    //NX
    Not16(in=zxOut, out=intermediateNx);
    //NX is not prio to ZX
    Mux16(a=zxOut, b=intermediateNx, sel=nx, out=nxOut);
    //ZY
    Mux16(a=y, b=false, sel=zy, out=zyOut);
    //NY
    Not16(in=zyOut, out=intermediateNy);
    //NY is not prio to ZY
    Mux16(a=zyOut, b=intermediateNy, sel=ny, out=nyOut);

    //f
    And16(a=nxOut, b=nyOut, out=nxAndnnY);
    Add16(a=nxOut, b=nyOut, out=nxPlusnY);
    Mux16(a=nxAndnnY, b=nxPlusnY, sel=f, out=fOut);
    //no
    Not16(in=fOut, out=nofOut);
    
    Mux16(a=fOut, b=nofOut, sel=no, out[0..7]=lastOutHalfDown, out[8..15]=lastOutHalfUp);
    // Final output
    Or16(a[0..7]=lastOutHalfDown, a[8..15]=lastOutHalfUp, b=false, out=out);

    //ng
    And16(a[0..7]=lastOutHalfDown, a[8..15]=lastOutHalfUp, b=true, out[15]=ng);
    //zr
    Or8Way(in=lastOutHalfDown, out=or0);
    Or8Way(in=lastOutHalfUp, out=or1);
    Or(a=or0, b=or1, out=nzr);
    Not(in=nzr, out=zr);
}
