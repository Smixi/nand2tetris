// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    // Increment
    Inc16(in=lastOut, out=incr);
    Mux16(a=lastOut, b=incr, sel=inc, out=incrOrNot);
    // Handle load
    Mux16(a=incrOrNot, b=in, sel=load, out=pcOut);
    // Handle reset
    Mux16(a=pcOut, b=false, sel=reset, out=toReg);
    Register(in=toReg, out=out, load=true, out=lastOut);
}
